env = Environment(CPPPATH = ['src', '../libgambatte/include'],
                  LIBS = ['gambatte'],
                  LIBPATH = '../libgambatte',
                  CFLAGS = ARGUMENTS.get('CFLAGS', '-Wall -Wextra -O2 -fomit-frame-pointer'),
                  CXXFLAGS = ARGUMENTS.get('CXXFLAGS', ARGUMENTS.get('CFLAGS', '-Wall -Wextra -O2 -fomit-frame-pointer') + ' -fno-exceptions -fno-rtti'))

env.ParseConfig('sdl-config --cflags --libs')

sourceFiles = Split('''
			src/gambatte_sdl.cpp
			src/parser.cpp
			src/syncfunc.cpp
		   ''')

conf = env.Configure()

if conf.CheckLib('z'):
	env.Append(LIBS = ['z'])

conf.Finish()

env.Program('gambatte_sdl', sourceFiles)
